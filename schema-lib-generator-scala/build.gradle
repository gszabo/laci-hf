buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.guava:guava:21.0"
    }
}

import com.google.common.base.CaseFormat
import com.google.common.io.Files

apply plugin: "maven"
apply plugin: "scala"

group "com.prezi.homeassignment"
version "0.1"

repositories {
    mavenCentral()
    build
}

configurations {
    generatedCompile
}

dependencies {
    compile "org.scala-lang:scala-library:2.11.8"
    compile "org.scala-lang:scala-compiler:2.11.8"
    compile "io.argonaut:argonaut_2.11:6.1"
    compile "commons-io:commons-io:2.6"
    testCompile 'org.scalatest:scalatest_2.11:3.0.5'
    // TODO: add generated code dependencies here like this:
    // generatedCompile "org:awesomelib:4.55"
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters = ["-unchecked", "-deprecation", "-feature", "-Xfatal-warnings", "-encoding", "utf8"]
}

task jarAllGenerated

fileTree("$projectDir/src/main/resources/schemas").each {
    if (it.getAbsolutePath().endsWith(".json")) {
        codeGenTask name: Files.getNameWithoutExtension(it.getName()) + "-lib", schema: it.getAbsolutePath()
    }
}

def codeGenTask(Map<String, Object> props) {
    def configurationName = props.name
    def taskName = CaseFormat.LOWER_HYPHEN.to(CaseFormat.UPPER_CAMEL, props.name)
    def dirSegment = props.name
    def providedSrcDir = "${projectDir}/src/provided/java"
    def outSrcDir = "${projectDir}/build/generated-${dirSegment}-src"
    def outBuildDir = "${projectDir}/build/${dirSegment}-classes"
    def outJarDir = "${projectDir}/build/${dirSegment}-jar"
    def outJarBaseName = "${dirSegment}"

    configurations.create(configurationName).extendsFrom(configurations.generatedCompile)

    def generateSourcesTask = task("generate${taskName}Sources", type: JavaExec) {
        dependsOn classes
        inputs.file props.schema
        outputs.dir outSrcDir
        main = 'com.prezi.homeassignment.Main'
        args = [props.schema, outSrcDir]
        classpath sourceSets.main.runtimeClasspath
    }

    def compileJavaTask = task("compileJava${taskName}", type: JavaCompile) {
        dependsOn generateSourcesTask
        source (providedSrcDir, outSrcDir)
        classpath = configurations.generatedCompile
        destinationDir = file(outBuildDir)
    }

    def jarTask = task("jar${taskName}", type: Jar) {
        dependsOn compileJavaTask
        from compileJavaTask.destinationDir
        baseName = outJarBaseName
        destinationDir = file(outJarDir)
    }

    def buildTask = task("build${taskName}") {
        dependsOn compileJavaTask
    }

    tasks.build.dependsOn(buildTask)
    tasks.jarAllGenerated.dependsOn(jarTask)

    artifacts.add(configurationName, file(jarTask.archivePath)) {
        builtBy jarTask
    }
}
